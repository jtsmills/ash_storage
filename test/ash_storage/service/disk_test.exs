defmodule AshStorage.Service.DiskTest do
  use ExUnit.Case, async: true

  alias AshStorage.Service.Context
  alias AshStorage.Service.Disk

  @moduletag :tmp_dir

  setup %{tmp_dir: tmp_dir} do
    ctx = Context.new(root: tmp_dir, base_url: "http://localhost:4000/storage")
    {:ok, ctx: ctx, root: tmp_dir}
  end

  describe "upload/3" do
    test "uploads binary data", %{ctx: ctx, root: root} do
      assert :ok = Disk.upload("test.txt", "hello world", ctx)
      assert File.read!(Path.join(root, "test.txt")) == "hello world"
    end

    test "uploads iolist data", %{ctx: ctx, root: root} do
      assert :ok = Disk.upload("test.txt", ["hello", " ", "world"], ctx)
      assert File.read!(Path.join(root, "test.txt")) == "hello world"
    end

    test "uploads file stream", %{ctx: ctx, root: root} do
      source = Path.join(root, "source.txt")
      File.write!(source, "streamed content")

      assert :ok = Disk.upload("dest.txt", File.stream!(source), ctx)
      assert File.read!(Path.join(root, "dest.txt")) == "streamed content"
    end

    test "creates nested directories as needed", %{ctx: ctx, root: root} do
      assert :ok = Disk.upload("a/b/c/test.txt", "nested", ctx)
      assert File.read!(Path.join(root, "a/b/c/test.txt")) == "nested"
    end
  end

  describe "download/2" do
    test "downloads an existing file", %{ctx: ctx, root: root} do
      File.write!(Path.join(root, "test.txt"), "hello")
      assert {:ok, "hello"} = Disk.download("test.txt", ctx)
    end

    test "returns error for missing file", %{ctx: ctx} do
      assert {:error, :not_found} = Disk.download("nonexistent.txt", ctx)
    end
  end

  describe "delete/2" do
    test "deletes an existing file", %{ctx: ctx, root: root} do
      path = Path.join(root, "test.txt")
      File.write!(path, "hello")

      assert :ok = Disk.delete("test.txt", ctx)
      refute File.exists?(path)
    end

    test "returns ok for missing file", %{ctx: ctx} do
      assert :ok = Disk.delete("nonexistent.txt", ctx)
    end
  end

  describe "exists?/2" do
    test "returns true for existing file", %{ctx: ctx, root: root} do
      File.write!(Path.join(root, "test.txt"), "hello")
      assert {:ok, true} = Disk.exists?("test.txt", ctx)
    end

    test "returns false for missing file", %{ctx: ctx} do
      assert {:ok, false} = Disk.exists?("nonexistent.txt", ctx)
    end
  end

  describe "url/2" do
    test "generates a URL with the base_url and key", %{ctx: ctx} do
      assert Disk.url("abc/test.txt", ctx) == "http://localhost:4000/storage/abc/test.txt"
    end
  end

  describe "direct_upload/2" do
    test "generates upload URL and headers", %{ctx: ctx} do
      assert {:ok, %{url: url, headers: headers}} = Disk.direct_upload("my-key", ctx)
      assert url == "http://localhost:4000/storage/disk/my-key"
      assert headers["content-type"] == "application/octet-stream"
    end

    test "uses provided content_type", %{root: root} do
      ctx =
        Context.new(
          root: root,
          base_url: "http://localhost:4000/storage",
          content_type: "image/png"
        )

      assert {:ok, %{headers: headers}} = Disk.direct_upload("my-key", ctx)
      assert headers["content-type"] == "image/png"
    end
  end

  describe "upload then download round-trip" do
    test "binary data survives round-trip", %{ctx: ctx} do
      content = :crypto.strong_rand_bytes(1024)
      key = "round-trip-#{System.unique_integer([:positive])}"

      assert :ok = Disk.upload(key, content, ctx)
      assert {:ok, ^content} = Disk.download(key, ctx)
    end
  end
end
